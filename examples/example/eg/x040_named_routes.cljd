(ns example.eg.x040-named-routes
  "Port of CLJD sample.navigate-named-routes...
      Port of https://docs.flutter.dev/cookbook/navigation/named-routes.html"
  (:require
    [tilton.fmx.api :as fx
     :refer [dart-cb scaffold app-bar elevated-button text center material-app ctxd?]]
    ["package:flutter/material.dart" :as m]))

(defn first-screen [ctx]
  (scaffold
    {:appBar (app-bar
               {:title (m/Text "First Screen")})}
    (center
      (elevated-button
        {:onPressed (dart-cb []
                      (ctxd? :x040-launch me ctx)
                      (.pushNamed (-> ctx m/Navigator.of) "/second"))}
        (text "Launch Screen")))))

(defn second-screen [ctx]
  (scaffold
    {:appBar (app-bar
               {:title (m/Text "Second Screen")})}
    (center
      (elevated-button
        {:onPressed (dart-cb []
                      (ctxd? :x040-seco me ctx)
                      (-> ctx m/Navigator.of .pop))}
        (fx/text "Go Back!")))))

(defn make-app []
  (material-app
    {:title        "Navigation Basics"
     :initialRoute "/"
     :routes (into {}
               (for [[k gen] {"/"       first-screen
                              "/second" second-screen}]
                 [k (fn ^m/Widget k [ctx]
                      (fx/fx-render ctx
                        (gen ctx)))]))}))