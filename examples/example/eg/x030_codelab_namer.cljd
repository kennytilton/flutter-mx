(ns example.eg.x030-codelab-namer
  (:require
    ["package:flutter/material.dart" :as m]
    ["package:flutter/widgets.dart" :as w]
    ["package:english_words/english_words.dart" :as en]
    [tilton.mx.api :refer [dp cF cFn cI mget mset! mswap! fasc fmu fmuv] :as mx]
    [tilton.mx.model.peek :refer [mname]]
    [tilton.fmx.core :refer [as-dart-callback] :as fx]))

(defn big-card []
  (fx/card {:color (fx/in-my-context [me ctx]
                     (-> (m.Theme/of ctx) .-colorScheme .-primary))}
    {:name :bigc
     :word-pair (cF (mget (fasc :app) :word-pair))}
    (fx/padding {:padding (m/EdgeInsets.all 20)}
      (fx/text {:style          (fx/in-my-context [me ctx]
                                  (.copyWith (.-displayMedium (.-textTheme (m.Theme/of ctx)))
                                    .color (-> (m.Theme/of ctx) .-colorScheme .-onPrimary)))
                :semanticsLabel (cF (let [wp (mget (fasc :bigc) :word-pair)]
                                      (dp :wp!!!!!!!!!!! wp)
                                      (.-asPascalCase ^en/WordPair wp)))}
        (.-asCamelCase ^en/WordPair (mget (fasc :bigc) :word-pair))))))

(defn generator-page []
  (fx/center
    (fx/column {:mainAxisAlignment m.MainAxisAlignment/center}
      (big-card)
      (m/SizedBox .height 10)
      (fx/row {:mainAxisSize m/MainAxisSize.min}
        (fx/elevated-button+icon
          {:label     (fx/text "Like")
           :icon      (cF (if ((mx/fmav :app :favorites)
                               (mx/fmav :app :word-pair))
                            (m/Icon m/Icons.favorite)
                            (m/Icon m/Icons.favorite_border)))
           :onPressed (as-dart-callback []
                        (let [word-pair (mx/fmav :app :word-pair)
                              favs (mx/fmav :app :favorites)]
                          (mswap! (fasc :app) :favorites
                            (if (favs word-pair)
                              disj conj) word-pair)))})
        (m/SizedBox .width 10)
        (fx/elevated-button
          {:onPressed (as-dart-callback []
                        (mset! (fasc :app) :word-pair (en/WordPair.random)))}
          (fx/text "Next"))))))

(defn favorites-page [favs]
  (if (empty? favs)
    (fx/center
      (fx/text "No favorites yet."))
    (fx/list-view
      (fx/padding
        {:padding (m/EdgeInsets.all 20)}
        (fx/text (str "You have " (count favs) " favorite"
                   (if (= 1 (count favs)) ":" "s:"))))
      (doall (for [fav favs]
               (fx/list-tile
                 {:leading (m/Icon m/Icons.favorite)
                  :title   (m/Text (.-asCamelCase ^en/WordPair fav))}))))))

(defn make-app []
  (let [title "Codelab: Namer App"]
    (fx/material-app
      {:title title
       :theme (m/ThemeData
                .useMaterial3 true
                .colorScheme (m/ColorScheme.fromSeed
                               .seedColor m/Colors.cyanAccent))}
      (fx/scaffold
        {:appBar (fx/app-bar {:title (fx/text title)})}
        {:name      :app
         :word-pair (cI (en/WordPair.random))
         :favorites (cI #{})}
        (fx/row
          (fx/safe-area
            (fx/navigation-rail
              {:extended              false
               :selectedIndex         (cI 0)
               :onDestinationSelected (as-dart-callback [value]
                                        (mset! me :selectedIndex value))}
              {:name :nav}
              (doall (for [[icon label] [[m/Icons.home "Home"]
                                         [m/Icons.favorite "Favorites"]]]
                       (m/NavigationRailDestination
                         .icon (m/Icon icon)
                         .label (m/Text label))))))
          (fx/expanded
            (fx/container
              {:color (fx/in-my-context [me ctx]
                        (-> (m.Theme/of ctx) .-colorScheme .-primaryContainer))}
              (case (fmuv :nav :selectedIndex)
                0 (generator-page)
                1 (favorites-page
                    (fmuv :app :favorites))))))))))