(ns example.eg.x035-timer-poll
  (:require
    ["dart:math" :as math]
    ["dart:async" :as async]
    ["package:flutter/widgets.dart" :as w]
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]
    ["package:http/http.dart" :as dht]
    [tilton.mx.cell.base :as cb]
    ;[tilton.mx.model.core :refer [with-par]]
    [tilton.mx.api :refer [with-par dp dpx cF cF+ cI mget mswap! mset! fasc fm* fmuval fmu minfo] :as mx]
    [tilton.fmx.api :refer [as-dart-callback] :as fx]))

;;; This is a half-baked example that shows a widget dispatching
;;; HTTP requests as cued by a timer.
;;;
;;; We demonstrate a "fetch" widget creating a timer and, when the user
;;; decides to stop fetching, we destroy the widget, triggering an
;;; on-quiesce handler on the cell that spawned the timer, to cancel the timer.
;;;
;;; The HTTP request alternates between checking fda.gov for adverse events on "dogs" (none) and "cats" (successful).

(defn make-app []
  (fx/material-app
    {:title "Flutter/MX Counter Demo"
     :theme (m/ThemeData .primarySwatch m.Colors/blue)}
    (fx/scaffold
      {:appBar               (fx/app-bar
                               {:title (m/Text "Flutter/MX Timer Life-Cycle")})
       :floatingActionButton (cF (fx/floating-action-button
                                   {:onPressed (as-dart-callback []
                                                 (mswap! (fmu :scaffo) :show-time? not))
                                    :tooltip   "Toggle Time"}
                                   (if (mget (fmu :scaffo) :show-time?)
                                     (m/Icon m.Icons/stop_rounded
                                       .color m.Colors/white)
                                     (m/Icon m.Icons/download_rounded
                                       .color  m.Colors/black))))}
      {:name       :scaffo
       :show-time? (cI true)}
      (fx/center
        (fx/wrap {:direction          m.Axis/vertical
                  :crossAxisAlignment m.WrapCrossAlignment/center
                  :spacing            14}
          (fx/container {}{:name :contnr}
            (when (mget (fasc :scaffo) :show-time?)
              (fx/text {:style (p/TextStyle .color m.Colors/black
                                 .fontSize 18.0)}
                {:name :shotime
                 :clock (cF+ [:debug false
                              :watch (fn [_ _ new old c]
                                       (dpx :shotime-watch-sees-new new))
                              :on-quiesce (fn [c]
                                            (when-let [tmr ^async/Timer (cb/c-value c)]
                                              (dp :cancelling-timer!!! tmr)
                                              (.cancel tmr)))]
                          (async/Timer.periodic (Duration .seconds 3)
                            (fn [^async/Timer tmr]
                              (dp :tmr-fires!!!! (.-tick tmr)(minfo me))
                              (mset! (fmu :counter) :value (.-tick tmr)))))}
                "Making fetch #")))
          (fx/text
            {:style (fx/in-my-context [me ctx]
                      (.-headline4 (.-textTheme (m.Theme/of ctx))))}
            {:name  :counter
             :value (cI 0)}
            (str (mx/my-value)))
          (fx/text {}
            {:name :ae-says
             :ae-lookup (cF+ [:async? true
                              :and-then? (fn [c lookup]
                                           (= 200 (.-statusCode ^dht/Response lookup)))]
                          (when-not (zero? (fmuval :counter))
                            (dht/get (.https Uri "api.fda.gov" "drug/event.json"
                                       {"limit"  "1"
                                        "search" (str "patient.drug.openfda.brand_name:"
                                                   (if (odd? (fmuval :counter))
                                                     "dogs" "cats"))}))))}
            (str "AE says " (mget me :ae-lookup))))))))