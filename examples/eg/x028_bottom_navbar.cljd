(ns eg.x028-bottom-navbar
  (:require
    [clojure.string :as str]
    [tiltontec.mx.api :refer [dp cF mget cI cF mset! mswap! fmu mkids] :as mx]

    [tiltontec.flutter-mx.core :as fx
     :refer [scaffold app-bar text center column hero material-app]]
    ["package:flutter/widgets.dart" :as w]
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]))

;;; Refined version of https://api.flutter.dev/flutter/material/BottomNavigationBar-class.html
;;;
;;; We deviate by making the nav bar more of a tool, with the allowed items defined elsewhere.
;;; Also, the initial setting of currentIndex now becomes symbolic, the keyword page ID, instead of
;;; an arbitrary integer.
;;;

(defn cl-position-if [coll test]
  (loop [[elt & more] coll
         idx 0]
    (cond
      (nil? elt) nil
      (test elt) idx
      :else (recur more (inc idx)))))

(defn make-app []
  (material-app
    {:title "Bottom Nav Bar Demo"}
    (scaffold
      {:appBar              (app-bar
                              {:title (m/Text "Bottom Navigation Bar Demo")})
       :bottomNavigationBar (fx/bottom-navigation-bar
                              {;; :type m.BottomNavigationBarType/fixed or .../shifting
                               ;; ^^^ we can specify fixed or shifting to override defaults, which
                               ;; are fixed for items <= 3, shifting above 3. Very strange.
                               :currentIndex      (cF (let [scaff (fmu :top-scaff)
                                                            curr-id (mget scaff :current-page-id)]
                                                        (cl-position-if (mget scaff :pages)
                                                          (fn [page] (= curr-id (:page-id page))))))
                               :selectedItemColor (.-shade800 m.Colors/amber)
                               :onTap             (fx/as-dart-callback [i]
                                                    (let [item (nth (mkids me) i)]
                                                      (mset! (fmu :top-scaff) :current-page-id
                                                        (mget item :page-id))))}
                              (mapv (fn [{:keys [page-id icon label bkg]}]
                                      (fx/bottom-navigation-bar-item
                                        {:icon            icon
                                         :label           label
                                         :backgroundColor bkg}
                                        {:page-id page-id}))
                                ;; todo why does not fasc work in next?
                                (mget (mx/fmu :top-scaff) :pages)))}
      {:name            :top-scaff
       :current-page-id (cI :home)
       :pages           [{:page-id :home
                          :icon    (m/Icon m/Icons.home)
                          :label   "Home"
                          :bkg     m.Colors/red}
                         {:page-id :business
                          :icon    (m/Icon m/Icons.business)
                          :label   "Business"
                          :bkg     m.Colors/green}
                         {:page-id :school
                          :icon    (m/Icon m/Icons.school)
                          :label   "School"
                          :bkg     m.Colors/purple}
                         ;; un-comment this guy to trigger default BottomNavigationBarType.shifting
                         ;; comment to get just three items, hence default BottomNavigationBarType.fixed.
                         ;; ps. All that can be overridden by specifying the :type directly. See above.
                         {:page-id :settings
                          :icon    (m/Icon m/Icons.settings)
                          :label   "Settings"
                          :bkg     m.Colors/pink}]}
      (center
        (column
          {:mainAxisAlignment m.MainAxisAlignment/spaceEvenly}
          (text {:style (p/TextStyle
                          .fontWeight m.FontWeight/bold
                          .fontSize 30.0)}
            (str "Nav page " (mget (mx/fasc :top-scaff) :current-page-id))))))))
