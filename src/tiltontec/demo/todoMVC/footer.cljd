(ns tiltontec.demo.todoMVC.footer
  (:require
    [tiltontec.util.base :refer [dprn dp dpx]]
    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.cell.core :refer [cF cF+ cF_ cI c_F cFonce]]
    [tiltontec.model.core :refer [mget mset! mswap! fasc] :as md]
    [tiltontec.mx-flutter.core :as fx]
    [tiltontec.demo.todoMVC.todo :refer [my-app] :as todo]

    ["dart:convert" :as convert]
    ["package:localstore/localstore.dart" :as ls]
    ["package:flutter/widgets.dart" :as w]
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]))

(defn items-remaining-display []
  (fx/text! {}
    {:name :items-left}
    (let [tds (todo/app-todos (my-app))
          left (remove #(mget % :completed) tds)]
      (str (count left) " item" (when (not= 1 (count left))
                                  "s")))))

(defn completion-filters []
  (fx/row
    {:mainAxisAlignment m.MainAxisAlignment/spaceEvenly}
    {:name      :completions
     :selection (cI :all)}
    (let [selector me ;; we give "me" a name reflecting that this widget will track the current selection
          choice (fn [completion-enum option$]
                   (fx/ink-well
                     {:onTap (fx/with-ctx+as-is [me ctx]
                               #(cty/with-fx-isolation
                                  (mset! (md/fasc :completions)
                                    :selection completion-enum)))}
                     (fx/container
                       {:decoration (cF (when (mget me :selected?)
                                          (m/BoxDecoration
                                            .border (.all m/Border .color (todo/title-red) .width 0.5)
                                            .borderRadius ^:const (m.BorderRadius/all (m.Radius/circular 4.0)))))}
                       {:completion-enum completion-enum
                        :selected?       (cF (= completion-enum
                                               (mget selector :selection)))}
                       (fx/padding
                         {:padding ^:const (m.EdgeInsets/all 3.0)}
                         (m/Text option$)))))]
      (list
        (choice :all "All")
        (choice :active "Active")
        (choice :done "Done")))))

(defn clear-completed-button []
  (fx/visibility!
    {:visible           (cF (if-let [tds (todo/app-todos (my-app))]
                              (boolean
                                (some #(mget % :completed) tds))
                              ;; nb might still be loading initially
                              false))
     ;; todo next: simpler visibility calls
     :maintainSize      true
     :maintainAnimation true
     :maintainState     true}
    (fx/text-button
      {:onPressed (fx/with-ctx+as-is [me ctx]
                    (fn []
                      (cty/with-fx-isolation
                        (md/mswap! (mget (md/fasc :app) :todo-db) :kids
                          (partial remove #(mget % :completed))))))}
      (fx/text "Clear done"))))