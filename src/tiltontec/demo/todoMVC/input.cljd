(ns tiltontec.demo.todoMVC.input
  (:require
    ["dart:convert" :as convert]
    ["package:localstore/localstore.dart" :as ls]
    ["package:flutter/widgets.dart" :as w]
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]

    [tiltontec.model.core :refer [fasc mget mset! mswap! mkids] :as md]
    [tiltontec.cell.core :refer [cF cF+ cF_ cI c_F cFonce]]
    [tiltontec.mx-flutter.core :as fx]
    [tiltontec.demo.todoMVC.common :refer [my-app my-todo-list as-dart-callback] :as cmm]
    [tiltontec.demo.todoMVC.todo :refer [td-completed] :as todo]

    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.util.base :refer [dprn dp dpx]]
    [clojure.string :as str]))

(defn todo-input []
  (fx/material {:elevation   20.0
                :color       m.Colors/white
                :shadowColor m.Colors/black}
    (fx/text-field {:autofocus   true
                    :focusNode   (m/FocusNode)
                    :decoration  (m/InputDecoration
                                   .hintText "What needs doing?"
                                   .border m.InputBorder/none
                                   .contentPadding (m.EdgeInsets/all 9.0))
                    :onSubmitted (cF (as-dart-callback [value]
                                       ;; blank or not we need to restore focus to the input per the spec
                                       (.requestFocus ^m/FocusNode (mget me :focusNode))
                                       ;; spec says to trim, and to silently ignore blank entry
                                       (let [trimmed (str/trim value)]
                                         (when-not (str/blank? trimmed)
                                           ;; --- non-blank entry, so we save, clear entry, and store
                                           (md/mset! me :value trimmed) ;; let text proxy know its cleaned value
                                           (.clear ^m/TextEditingController
                                             (md/mget me :controller))
                                           (let [td-lst (my-todo-list)]
                                             (mswap! td-lst :kids conj
                                               (todo/make-ToDo td-lst trimmed false)))))))
                    :controller  (cFonce (let [c (m/TextEditingController
                                                   .text (md/mget me :value))]
                                           ;; todo package this as a reusable
                                           (.addListener c
                                             (fn []
                                               (cty/with-mx-isolation
                                                 (md/mset! me :value (.-text c)))))
                                           c))}
      {:name  :todo-input
       :value (cI "" #_(str "test " (rand-int 99)))})))

(defn todo-toggle-all []
  (fx/visibility!
    {:visible               (cF (boolean
                                  (seq (todo/app-todos (my-app)))))
     :maintainSize          true
     :maintainSemantics     false
     :maintainInteractivity false
     :maintainAnimation     true
     :maintainState         true
     }
    (fx/icon-button
      {:onPressed (as-dart-callback []
                    (let [todos (todo/app-todos (my-app))
                          all-done? (every? td-completed todos)]
                      (doseq [td todos]
                        ;; todo do all these in one change pulse
                        (md/mset! td :completed (if all-done? false true)))))
       :icon      (m/Icon m.Icons/expand_more .color m.Colors/blueGrey)})))

(defn todo-controls []
  (fx/row
    (fx/expanded {:flex 1}
      (todo-toggle-all))
    ; --- the input ----
    (fx/expanded {:flex 9}
      (fx/padding
        {:padding (m.EdgeInsets/all 16.0)}
        (todo-input)))))