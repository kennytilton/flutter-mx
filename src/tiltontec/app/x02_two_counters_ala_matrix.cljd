(ns tiltontec.app.x02-two-counters-ala-matrix
  (:require
    [clojure.string :as str]
    [tiltontec.util.base :refer [dprn dp dpx]]
    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.cell.core :as cell :refer [cF cFnil cI]]
    [tiltontec.model.base :refer [md-cell]]
    [tiltontec.model.core :refer [make mx-par mget md-kids cFkids fmu fmuv] :as md]
    [tiltontec.mx-flutter.core :as fx]
    ["package:flutter/widgets.dart"
     :refer [Text State StatefulWidget StatelessWidget]]
    ["package:flutter/material.dart" :as m
     :refer [MainAxisAlignment Colors Theme Icon Icons ThemeData runApp AppBar State]]
    ["package:flutter/painting.dart"
     :refer [TextStyle]]))

(defn- build-counter [counter-key]
  (fx/expanded
    (fx/column
      {:mainAxisAlignment MainAxisAlignment/-center}
      {:name  :ctr-col
       ;; The column maintains its own state, aka "in-place state".
       ;; We find it useful to give every widget a "value" property for holding the primary value of a widget
       :value (cI 0)}
      (fx/text (str/capitalize (name counter-key)))
      (fx/text {:style (fx/cb-with-ctx [me ctx]
                         ;; we hacked the text widget a bit to look for callback functions on "style"
                         ;; so a style formula can tap the runtime context.
                         (-> (Theme/of ctx) .-textTheme .-displayLarge))}
        (str (md/fmuval :ctr-col)))
      (fx/elevated-button
        {:onPressed (fx/->CBAsIs
                      (fn ^void []
                        (md/mswap! (md/fm* :ctr-col) :value inc)))}
        (fx/text "+")))))

(defn make-app []
  (fx/material-app {:title "Two Counters (MX)"
                 :theme (cF (ThemeData .primarySwatch Colors/-blue))}
    (fx/scaffold
      {:appBar (AppBar .title (Text "Two Counters (MX)"))}
      (fx/center
        (fx/row
          {:mainAxisAlignment MainAxisAlignment/-center}
          (mapv #(build-counter %)
            [:left :right]))))))