(ns tiltontec.app.x03-physics-sim
  (:require
    [tiltontec.cell.core :as cell :refer [cF cI]]
    [tiltontec.model.core :refer [make mx-par mget md-kids cFkids fmu fmuv] :as md]
    [tiltontec.mx-flutter.corex :as fx]
    ["package:flutter/widgets.dart"
     :refer [Text State StatefulWidget StatelessWidget] :as w]
    ["package:flutter/physics.dart" :as p]
    ["package:flutter/material.dart" :as m
     :refer [MainAxisAlignment MediaQuery Colors Theme Icon AnimationController Icons ThemeData runApp AppBar State]]
    ["package:flutter/painting.dart"
     :refer [TextStyle Alignment]]))

(defn logo-recoil [me ctx ^m/Offset px-per-s]
  (let [size (.size (MediaQuery/of ctx))
        controller (fx/my-controller)
        animation ^#/(m/Animation Alignment) (.drive controller
                                               (m/AlignmentTween
                                                 :begin (mget me :dragAlignment)
                                                 :end m.Alignment/center))
        units-per-s (m/Offset (/ (.dx px-per-s) (.width size))
                      (/ (.dy px-per-s) (.height size)))
        unit-velocity (.distance units-per-s)
        spring ^:const (p/SpringDescription
                         :mass 30
                         :stiffness 1
                         :damping 1)
        simulation (p/SpringSimulation spring 0 1 (- unit-velocity))]
    (doto controller
      .clearListeners
      (.addListener #(md/mset! me :dragAlignment (.value animation))) ;; <==== dataflow trigger
      (.animateWith simulation))))

(defn draggable-card [& {:keys [drag-child]}]
  (fx/gesture-detector
    {:onPanDown   (cF (fx/MXFXAsIs
                        (fn [^m/DragDownDetails details]
                          (when-let [c (fx/my-controller)]
                            (.stop c)))))
     :onPanUpdate (fx/with-ctx-cb [me ctx]
                    (fx/MXFXAsIs
                      (fn [^m/DragUpdateDetails details]
                        (let [size (.size (MediaQuery/of ctx))]
                          (md/mswap! me :dragAlignment
                            #(.+ ^m/Alignment %
                               (m/Alignment
                                 (/ (-> details .delta .dx) (/ (.width size) 2))
                                 (/ (-> details .delta .dy) (/ (.height size) 2)))))))))
     :onPanEnd    (cF (fx/with-ctx-cb [me ctx]
                        (fx/MXFXAsIs
                          (fn [^m/DragEndDetails details]
                            (logo-recoil me ctx (-> details .velocity .pixelsPerSecond))))))

     }
    {:name          :draggable-card
     :dragAlignment (cI m.Alignment/center)
     :controller    (cF (when-let [st (fx/my-state)]
                          (m/AnimationController :vsync st)))
     :dispose       (fn [this me]
                      (when-let [con (fx/my-controller)]
                        (.dispose con)))}
    (fx/align
      {:alignment (cF (md/mdv! :draggable-card :dragAlignment))}
      {}
      (fx/card drag-child))))

(defn make-app []
  (let [title (str "Physics Animation")]
    (fx/material-app {:title title}
      (fx/scaffold
        {:appBar (fx/app-bar {:title (fx/text title)})}
        (draggable-card
          :drag-child (fx/flutter-logo {:size 128.0} {}))))))